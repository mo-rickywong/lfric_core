!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used
!-----------------------------------------------------------------------------
!> @brief    Routines to assign mesh maps to mesh_type objects.
!> @details  Mesh type objects are initially created with only
!!           the target mesh names for their inter-grid maps.
!!           This module setups up the correct source target meshes
!!           to add intergrid maps to, allowing that the mesh names
!!           may differ to the local mesh from which the inter-grid maps
!!           originate.
module add_mesh_map_mod

  use constants_mod, only: i_def, str_def, cmdi
  use log_mod,       only: log_event,         &
                           log_scratch_space, &
                           log_level_error,   &
                           log_level_debug

  use extrusion_mod,       only: extrusion_type,           &
                                 uniform_extrusion_type,   &
                                 geometric_extrusion_type, &
                                 quadratic_extrusion_type
  use local_mesh_mod,      only: local_mesh_type
  use mesh_collection_mod, only: mesh_collection
  use mesh_mod,            only: mesh_type
  use sci_query_mod,       only: check_lbc
  use ugrid_mesh_data_mod, only: ugrid_mesh_data_type

  implicit none

  private
  public :: assign_mesh_maps, add_mesh_map

contains

!> @brief Assign mesh maps to specied mesh_type objects.
!> @param[in] mesh_names Names of meshes in application mesh
!!                       collection to assign intergrid maps.
subroutine assign_mesh_maps( mesh_names )

  implicit none

  character(*), intent(in) :: mesh_names(:)

  character(str_def) :: local_mesh_name
  character(str_def) :: mesh_name_A, mesh_name_B

  character(str_def), allocatable :: target_mesh_names(:)
  character(str_def), allocatable :: local_mesh_names(:)

  type(mesh_type),       pointer :: mesh
  type(local_mesh_type), pointer :: local_mesh

  type(mesh_type),       pointer :: mesh_A
  type(mesh_type),       pointer :: mesh_B

  integer(i_def) :: i, j, k

  nullify( mesh, mesh_A, mesh_B, local_mesh )

  if (size(mesh_names) > 1) then

    !============================================================================
    ! 1.0 Acquire the local mesh object used to create mesh object.
    !============================================================================
    ! Names of the local_mesh_type object may differ from
    ! mesh_type object name provided.
    allocate(local_mesh_names(size(mesh_names)))
    do i=1, size(mesh_names)
      mesh => mesh_collection%get_mesh(mesh_names(i))

      if (.not. associated(mesh)) then
        if (check_lbc(mesh_names(i))) then
          cycle
        end if
      end if

      local_mesh => mesh%get_local_mesh()
      local_mesh_names(i) = local_mesh%get_mesh_name()

    end do

    !============================================================================
    ! 2.0 Assign maps to meshes
    !============================================================================
    ! Intergrid maps reference the local mesh objects that they were read/created
    ! from. The names of these local mesh objects may differ from the name
    ! of the mesh_type object. So the correct pairing of mesh_type names needs
    ! to be found from the connected local meshes.

    do i=1, size(mesh_names)
      mesh_name_A = mesh_names(i)

      ! Find all of the target local meshes associated with the local
      ! mesh object that this mesh object was extruded from.
      mesh => mesh_collection%get_mesh(mesh_name_A)

      if ( .not. associated(mesh)) then
        if (check_lbc(mesh_names(i))) then
          cycle
        end if
      end if

      local_mesh => mesh%get_local_mesh()
      local_mesh_name = local_mesh%get_mesh_name()

      call local_mesh%get_target_mesh_names(target_mesh_names)

      ! Find the name of the mesh_type object that was extruded from
      ! the local mesh object that corresponds to the specified
      ! local mesh target.
      if (allocated(target_mesh_names)) then

        do j=1, size(target_mesh_names)
          mesh_name_B = cmdi
          do k=1, size(local_mesh_names)
            if (local_mesh_names(k) == target_mesh_names(j)) then
              mesh_name_B = mesh_names(k)
              exit
            end if
          end do

          if (mesh_name_B /= cmdi) then
            mesh_A => mesh_collection%get_mesh(mesh_name_A)
            mesh_B => mesh_collection%get_mesh(mesh_name_B)
            call add_mesh_map( mesh_A, mesh_B )
          end if
        end do

        deallocate(target_mesh_names)
      end if
    end do

  end if

end subroutine assign_mesh_maps


!> @brief   Creates intergrid map between two mesh_type objects.
!> @details The meshes should contain valid local mesh intergrid maps.
!> @param[in] source_mesh  Source mesh object
!> @param[in] target_mesh  Target mesh object
subroutine add_mesh_map( source_mesh, target_mesh )

  implicit none

  type(mesh_type), intent(inout) :: source_mesh
  type(mesh_type), intent(inout) :: target_mesh

  ! Mesh tag names may be different but could point to the same mesh
  ! So check the IDs are not the same
  if (source_mesh%get_id() == target_mesh%get_id()) then
    write(log_scratch_space,'(A)')                  &
        'Unable to create intergrid map: Source('// &
         trim(source_mesh%get_mesh_name())//' and target('//    &
         trim(target_mesh%get_mesh_name())//') mesh IDs are the same'
    call log_event( log_scratch_space, log_level_error )
  end if

  call source_mesh % add_mesh_map (target_mesh)
  write(log_scratch_space,'(a,i0,a)')     &
      'Adding intergrid map "'//          &
       trim(source_mesh%get_mesh_name())//'"-->"'//  &
       trim(target_mesh%get_mesh_name())//'"'
  call log_event( log_scratch_space, log_level_debug )

  return
end subroutine add_mesh_map

end module add_mesh_map_mod
